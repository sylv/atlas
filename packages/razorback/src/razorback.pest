WHITESPACE    = _{ " " | "\t" | NEWLINE }
COMMENT       = _{ COMMENT_LINE | COMMENT_BLOCK }
COMMENT_LINE  = _{ "//" ~ (!NEWLINE ~ ANY)* ~ NEWLINE }
COMMENT_BLOCK = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }

script = _{ SOI ~ (tag | outer)* ~ EOI }
outer  = @{ (!("{" | "//") ~ ANY)+ }

name = @{ (ASCII_ALPHANUMERIC | "_" | ".")+ }

tag = _{ "{" ~ (assign | reference | call) ~ "}" }

option = { name ~ "=" ~ value }
call   = { name ~ (option)? ~ value* }

assign    = { "=" ~ name ~ value }
reference = { "$" ~ name }

string       = ${ "\"" ~ string_inner ~ "\"" }
string_inner = @{ string_char* }
string_char  =  {
    !("\"" | "\\") ~ ANY
  | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
}

boolean =  { "true" | "false" }
number  = @{ ("-"? ~ ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT+)?) }
null    =  { "null" }

array = { "[" ~ value? ~ ("," ~ value)*? ~ "]" }

object = {
    "{" ~ "}"
  | "{" ~ pair ~ ("," ~ pair)* ~ "}"
}
pair   = { (name | string) ~ ":" ~ value }

// `text {{tag}} {"string in a string for some reason"}`
template            = _{ "`" ~ template_inner ~ "`" }
template_inner      =  { (template_expression | template_string)* }
template_string     = ${ (!("`" | "{") ~ ANY)+ }
template_expression = _{ "{" ~ value ~ "}" }

value = _{ (object | tag | array | template | string | number | boolean | reference | null) }
